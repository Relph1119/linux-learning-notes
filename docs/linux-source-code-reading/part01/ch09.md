# 第9回 开启分页机制

代码路径：`boot/head.s`

## 9.1 开启分页

```nasm
after_page_tables:
	pushl $0		# These are the parameters to main :-)
	pushl $0
	pushl $0
	pushl $L6		# return address for main, if it decides to.
	pushl $_main
	jmp setup_paging
L6:
	jmp L6			# main should never return here, but
				# just in case, we know what happens.
```

开启分页机制，并跳转到`main`函数的代码，其中传入三个参数envp、argv指针和argc值。

## 9.2 分页设置

```nasm
setup_paging:
	movl $1024*5,%ecx		/* 5 pages - pg_dir+4 page tables */
	xorl %eax,%eax
	xorl %edi,%edi			/* pg_dir is at 0x000 */
	cld;rep;stosl
	movl $pg0+7,_pg_dir		/* set present bit/user r/w */
	movl $pg1+7,_pg_dir+4		/*  --------- " " --------- */
	movl $pg2+7,_pg_dir+8		/*  --------- " " --------- */
	movl $pg3+7,_pg_dir+12		/*  --------- " " --------- */
	movl $pg3+4092,%edi
	movl $0xfff007,%eax		/*  16Mb - 4096 + 7 (r/w user,p) */
	std
1:	stosl			/* fill pages backwards - more efficient :-) */
	subl $0x1000,%eax
	jge 1b
	xorl %eax,%eax		/* pg_dir is at 0x0000 */
	movl %eax,%cr3		/* cr3 - page directory start */
	movl %cr0,%eax
	orl $0x80000000,%eax
	movl %eax,%cr0		/* set paging (PG) bit */
	ret			/* this also flushes prefetch-queue */
```

**分页举例：** 线性地址是`0000000011_0100000000_000000000000`

**具体分页步骤：**
1. 前10位表示页目录项`0000000011`，即页目录表中的第3号页目录项。
2. 中间10位表示页表项目`0100000000`，即3号页表的第256项。
3. 后12位表示页内偏移`000000000000`。
4. 所以内存的物理地址就等于页目录项+页表号+页内偏移地址。

**基本概念：**
- 这种页表方案叫做二级页表
- 第一级叫做页目录表PDE
- 第二级叫做页表PTE

上述代码详解：
- 由于内存上限为16MB，所以16MB=4（页表数）*1024（页表项数）*4KB（一页大小）
- 将页目录表和页表都放在整个内存布局的开头，覆盖开头的system代码。
- 设置各个页表的起始内存地址。

```nasm
.org 0x1000
pg0:
.org 0x2000
pg1:
.org 0x3000
pg2:
.org 0x4000
pg3:
.org 0x5000
```

- CR3寄存器保存页目录表的起始地址，通过页目录表可以找到所有的页表。
- `$pg0+7`表示页表地址为0x1000，页属性为0x07，表示该页存在（P=1）、用户可读写（RW=1）、特权为用户态（US=1）。
- 之后的每行表示填充4个页表的每一项，一共填充4096项。

## 9.3 分段机制和分页机制

- 分段机制：为每个程序或任务提供单独的代码段（DS）、数据段（DS）、栈段（SS），使其不会互相干扰。
- 分页机制：开机后，分页机制默认处于关闭状态，当开启后，设置页目录表和页表，主要是可以按需使用物理内存，同时在多任务时起到隔离作用。
- 逻辑地址：代码中给出的地址，包含段选择子和偏移地址。
- 线性地址：通过分段机制，将逻辑地址转换后的地址。
- 物理地址：内存中实际存在的地址，也称为物理地址空间。
- 虚拟地址：如果开启了分页，线性地址将被视为虚拟地址，通过分页机制转换为物理地址。



